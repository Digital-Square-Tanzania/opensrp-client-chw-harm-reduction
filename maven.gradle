apply plugin: 'maven-publish'
apply plugin: 'signing'

def isReleaseBuild() {
    return project.version.toString().contains("SNAPSHOT") == false
}

def sonatypeRepositoryURL
def mavenLocalFlag = hasProperty('mavenLocal') ? mavenLocal.toBoolean() : true;

if (isReleaseBuild()) {
    println 'PROCESSING MAVEN ' + (mavenLocalFlag ? 'LOCAL' : 'REMOTE') + ' RELEASE BUILD VERSION ' + project.VERSION_NAME + '...'
    sonatypeRepositoryURL = hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else {
    println 'PROCESSING MAVEN ' + (mavenLocalFlag ? 'LOCAL' : 'REMOTE') + ' SNAPSHOT BUILD VERSION ' + project.VERSION_NAME + '...'
    sonatypeRepositoryURL = hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"

}

def getRepositoryPassword() {
    return hasProperty('sonatypePassword') ? sonatypePassword : ""
}

def getRepositoryUsername() {
    return hasProperty('sonatypeUsername') ? sonatypeUsername : ""
}

afterEvaluate { project ->
    task androidJavadocs(type: Javadoc) {
        // Intentionally empty: configure sources/classpath in the module if needed.
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        archiveClassifier.set('javadoc')
    }

    task androidSourcesJar(type: Jar) {
        archiveClassifier.set('sources')
    }

    publishing {
        publications.withType(MavenPublication).configureEach {
            artifact(androidSourcesJar)
            artifact(androidJavadocsJar)
            pom {
                name.set(POM_SETTING_NAME)
                description.set(POM_SETTING_DESCRIPTION)
                url.set(POM_SETTING_URL)
                scm {
                    url.set(POM_SETTING_SCM_URL)
                    connection.set(POM_SETTING_SCM_CONNECTION)
                    developerConnection.set(POM_SETTING_SCM_DEV_CONNECTION)
                }
                licenses {
                    license {
                        name.set(POM_SETTING_LICENCE_NAME)
                        url.set(POM_SETTING_LICENCE_URL)
                        distribution.set(POM_SETTING_LICENCE_DIST)
                    }
                }
                developers {
                    developer {
                        id.set(POM_SETTING_DEVELOPER_ID)
                        name.set(POM_SETTING_DEVELOPER_NAME)
                    }
                }
            }
        }
        if (!mavenLocalFlag) {
            repositories {
                maven {
                    url = uri(sonatypeRepositoryURL)
                    credentials {
                        username = getRepositoryUsername()
                        password = getRepositoryPassword()
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications
    }

    if (project.extensions.findByName("nexusStaging") != null) {
        nexusStaging {
            username getRepositoryUsername()
            password getRepositoryPassword()
        }
    } else if (rootProject.extensions.findByName("nexusStaging") != null) {
        rootProject.nexusStaging {
            username getRepositoryUsername()
            password getRepositoryPassword()
        }
    }
}
